# MMS data, set solution and XSs as desired
phi_mms(x,y) = 1 + sin(2*x)^2 * cos(3*y)^2
D_mms(x,y) = 1 + 1e-2*x*y
SigmaA_mms(x,y) = 0.10*(1 + x - 0.5*y)


READ_MESH square-$2-$3-$4.msh DIMENSIONS 2
PROBLEM neutron_diffusion

DEFAULT_ARGUMENT_VALUE 1 dirichlet # BCs = dirichlet/neumann
DEFAULT_ARGUMENT_VALUE 2 tri3      # shape = tri3/tri6/quad4/quad8/quad9
DEFAULT_ARGUMENT_VALUE 3 struct    # algorithm = struct/frontal/delaunay
DEFAULT_ARGUMENT_VALUE 4 8         # refinement factor = 1/2/3/4...
DEFAULT_ARGUMENT_VALUE 5 0         # write vtk? = 0/1

# read the results of the symbolic derivatives
INCLUDE neutron-square-s.fee

# set the PDE coefficients and BCs we just read above
D1(x,y) = D_mms(x,y)
SigmaA1(x,y) = SigmaA_mms(x,y)
S1(x,y) = s_mms(x,y)

# set the BCs (depending on $1)
INCLUDE neutron-square-bc-$1.fee

SOLVE_PROBLEM   # this line should be self-explanatory 

# output
PHYSICAL_GROUP bulk DIM 2 
h = sqrt(bulk_area/cells)

# L-2 error
INTEGRATE (phi1(x,y)-phi_mms(x,y))^2 RESULT e_2
error_2 = sqrt(e_2)

# L-inf error
FIND_EXTREMA abs(phi1(x,y)-phi_mms(x,y)) MAX error_inf

PRINT %.6f log(h) log(error_inf) log(error_2) %g $4 cells nodes %.2f 1024*memory() wall_time()

IF $5
  WRITE_MESH neutron-square_$1-$2-$3-$4.vtk phi1 S1 phi_mms phi1(x,y)-phi_mms(x,y)
ENDIF
