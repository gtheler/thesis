PROBLEM GROUPS 1 DIMENSIONS 2 MESH tres-pescaditos-simplex.msh
SCHEME ELEMENTS

INCLUDE xs.mil

x1 = 0.2*A    # ubicacion fija del primer pescadito
y1 = -0.1*A

x2 = 0        # ubicacion fija del segundo pescadito
y2 = -0.15*A

# guess inicial para x3 y3 en el tercer cuadrante para 
# lustrar como milonga lo lleva al segundo
x3 = -0.6*A
y3 = -0.6*A

TEMPLATE tres-pescaditos.tpl tres-pescaditos-simplex.geo A a lc x1 y1 x2 y2 x3 y3
SHELL "gmsh -v 0 -2 tres-pescaditos-simplex.geo"
SOLVE_PROBLEM

# definimos una funcion que dependa de la posicion del nuevo pescadito
rho0 = (nuSigmaF_1(0,0.9*A) - SigmaA_1(0,0.9*A) - D_1(0,0.9*A)*(2.4048/A)^2)/nuSigmaF_1(0,0.9*A)
rho = (keff-1)/keff
deltarho(x3,y3) := 1e5*(rho - rho0)

# y le pedimos a milonga que la minimice con el algoritmo
# de los simplex que no necesita evaluar gradientes
MINIMIZE deltarho ALGORITHM nmsimplex2 STEP a a

# imprimimos en la salida estandar las soluciones parciales
OUTPUT_FILE out tres-pescaditos-simplex.dat
PRINT FILE out x3 y3 deltarho(x3,y3)
PRINT x3 y3 deltarho(x3,y3)

# y las ubicaciones de los dos pescaditos para dibujar
OUTPUT_FILE fijos pescaditos-fijos.dat
PRINT CONDITION done_outer x1 y1 FILE fijos
PRINT CONDITION done_outer x2 y2 FILE fijos
